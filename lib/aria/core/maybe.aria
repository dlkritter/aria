# SPDX-License-Identifier: Apache-2.0
flag: no_std;

extension Maybe {
    func apply(f) {
        match this {
            case Some(x) => { return Maybe::Some(f(x)); },
            case None => { return Maybe::None; }
        }
    }

    func hash() {
        match this {
            case Some(x) => { return x.hash(); },
        } else {
            return 0;
        }
    }

    func unwrap_or(x) {
        match this {
            case Some(y) => { return y; },
            case None => { return x; }
        }
    }

    func prettyprint() {
        match this {
            case None => {
                return "None";
            },
            case Some(value) => {
                return "Some({0})".format(value);
            }
        }
    }

    # these helpers are built by the Aria compiler, but since Maybe is a builtin type we need to provide them here
    func is_Some() {
        match this {
            case Some(_) => { return true; },
        } else {
            return false;
        }
    }

    func is_None() {
        match this {
            case None => { return true; },
        } else {
            return false;
        }
    }

    func unwrap_Some() {
        match this {
            case Some(value) => { return value; },
        } else {
            assert false;
        }
    }
}
