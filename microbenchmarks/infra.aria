# SPDX-License-Identifier: Apache-2.0
import aria.range.int_extension;

struct Timer {
    type func new(f) {
        return alloc(This) {
            .fn = f,
            .samples = []
        };
    }

    func run(n=1) {
        val start = now();
        for _ in 0.to(n) {
            this.fn();
        }
        val end = now();
        val duration = (end - start)/1000.0;
        this.samples.append(duration);
        return duration;
    }

    func average() {
        return this.samples.sum() / this.samples.len();
    }

    func max() {
        return this.samples.max();
    }

    func min() {
        return this.samples.min();
    }
}

# include this mixin and provide a func test() to create a microbenchmark
mixin Microbenchmark {
    type func new(name) = alloc(This) { .name };

    # override this if you need to prepare data before running the benchmark
    func prepare() {}

    # override this if you need to clean up after running the benchmark
    func teardown() {}

    func run(n=24) {
        assert hasattr(this, "test");
        if !hasattr(this, "timer") {
            this.timer = Timer.new(this.test);
        }

        for _ in 0.to(n) {
            this.prepare();
            println("{1} duration: {0:.3}s".format(this.timer.run(), this.name));
            this.teardown();
        }
        println("{0} min: {1:.3}s avg: {2:.3}s max: {3:.3}s".format(
            this.name,
            this.timer.min()??,
            this.timer.average(),
            this.timer.max()??
        ));
    }
}
